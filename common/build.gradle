plugins {
    id 'xplat-common'
    id 'net.neoforged.moddev.legacyforge'
}

legacyForge {
    mcpVersion = project.minecraft_version

    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    // validateAccessTransformers = true

    parchment {
        minecraftVersion = project.parchment_minecraft_version
        mappingsVersion = project.parchment_mappings_version
    }
}

dependencies {
    // //////////// Mixin ////////////
    compileOnly "org.spongepowered:mixin:$mixin_version"
    compileOnly "io.github.llamalad7:mixinextras-common:$mixin_extras_version"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}
